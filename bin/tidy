#!/usr/bin/perl
use Cwd qw(realpath);
use CHI;
use File::Basename;
use File::Find;
use File::Signature;
use File::Slurp;
use Perl::Tidy;
use strict;
use warnings;

my $root = dirname(dirname(realpath($0)));
my $rcfile = "$root/perltidyrc";

my $pattern = shift(@ARGV) || '.';

my @files;
find ({wanted => sub { push(@files, $_) if /CHI/ && /\.pm$/ && /$pattern/ }, no_chdir => 1}, "$root/lib");

my $base_sig = join('; ', map { File::Signature->new($_) } ($rcfile, $INC{'Perl/Tidy.pm'}));

sub sig
{
    my ($file) = @_;

    return join('; ', $base_sig, File::Signature->new($file));
}

my $cache = CHI->new(driver => 'FastMmap', root_dir => "$root/data/cache", namespace => 'tidy');

foreach my $file (@files) {
    if (($cache->get($file) || '') ne sig($file)) {
        print "$file\n";
        Perl::Tidy::perltidy(source => $file, destination => \my $result, perltidyrc => $rcfile);
        write_file($file, $result);
        $cache->set($file, sig($file));
    }
}

1;
