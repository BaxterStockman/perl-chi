#!/usr/bin/perl
use Cwd qw(realpath);
use CHI;
use File::Basename;
use File::Find;
use File::Signature;
use File::Slurp;
use File::Temp qw(tempfile);
use IPC::System::Simple qw(run);
use Perl::Tidy;
use strict;
use warnings;

my $root = dirname(dirname(realpath($0)));
my $tidyrc = "$root/perltidyrc";

my $pattern = shift(@ARGV) || '.';

my @files;
find ({wanted => sub { push(@files, $_) if /CHI/ && /\.pm$/ && /$pattern/ }, no_chdir => 1}, "$root/lib");

sub sig
{
    my ($file) = @_;

    return File::Signature->new($file) . '';
}

my $cache = CHI->new(driver => 'FastMmap', root_dir => "$root/data/cache", namespace => 'tidy');
if (($cache->get($tidyrc) || '') ne sig($tidyrc)) {
    $cache->clear();
}

foreach my $file (@files) {
    next if (($cache->get($file) || '') eq sig($file));
    print "$file\n";
    Perl::Tidy::perltidy(source => $file, destination => \my $result, perltidyrc => $tidyrc);
    write_file($file, $result);
    $cache->set($file, sig($file) );
}

if (!$cache->get($tidyrc)) {
    $cache->set($tidyrc, sig($tidyrc) );
}

1;
